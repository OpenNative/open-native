plugins {
    id 'com.android.library'
}

def safeExtGet = {prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def androidXCoreVersion = safeExtGet("androidXCoreVersion","1.8.0");
def androidXCompatVersion = safeExtGet("androidXCompatVersion","1.5.1");
def okHttpVersion = safeExtGet("okHttpVersion", null);

repositories {
  google()
  mavenCentral()
}

android {
    compileSdk safeExtGet("compileSdkVersion",31)
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion',21)
        targetSdkVersion safeExtGet("targetSdkVersion",31)
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

  sourceSets {
    main {
      if (okHttpVersion) {
        java.srcDirs += ['src/network/java']
      }
    }
  }
}



dependencies {
    api("androidx.core:core:${androidXCoreVersion}")
    api("androidx.appcompat:appcompat:${androidXCompatVersion}")
    api("androidx.appcompat:appcompat-resources:${androidXCompatVersion}")
    api('com.facebook.infer.annotation:infer-annotation:0.18.0')
    api("com.facebook.yoga:proguard-annotations:1.19.0")
    //api('com.google.code.findbugs:jsr305:3.0.2')

    if (okHttpVersion) {
      api("com.squareup.okhttp3:okhttp:${okHttpVersion}")
      api("com.squareup.okhttp3:okhttp-urlconnection:${okHttpVersion}")

    }
    api("com.facebook.fresco:fresco:2.5.0")
}


